execonce "String.mac";

union perolValue {
	Bool b;
	Char c;
	Int i;
}

register Perol {
	Int tipo;
	perolValue val;
}

dec holandSort(Perol[] @peroles, Int N);
dec printPerol(Perol @p);

let Perol[5] peroles;
let Int N, i, tipo;
let Char option;

while True do 
	print("Desea organizar los peroles? [Y/n] ");
	option = readc();
	if option == 'n' || option == 'N' then 
		print("Hasta luego!");
		break;
	done 

	print("Indique el numero de peroles a organizar: ");
	N = readi();

	if N > 20 then 
		print("No se pueden organizar mas de 20 peroles.");
		continue;
	done

	let Bool b;
	i = 0;
	while i < N do 
		print("Indique el tipo del perol: ");
		print("1. Booleano");
		print("2. Caracter");
		print("3. Entero");
		tipo = readi();
		peroles[i].tipo = tipo;

		if tipo == 1 then 
			print("El valor del perol es True? [Y/n] ");
			option = readc();
			peroles[i].val.b = option != 'n' && option != 'N';

		elsif tipo == 2 then 
			print("Indique el caracter: ");
			peroles[i].val.c = readc();

		elsif tipo == 3 then 
			print("Indique el entero: ");
			peroles[i].val.i = readi();

		else 
			print("Tipo invalido");
			continue;
		done 

		printPerol(peroles[i]);
		i = i + 1;
	done

	holandSort(peroles, N);

	print("%cImprimimos los peroles ordenados:", chars=['\n']);
	for (i; 0; N) do 
		if peroles[i].tipo == 1 then 
			print("BOOL: ", end="");
			if peroles[i].val.b then print("True"); 
			else print("False");
			done

		elsif peroles[i].tipo == 2 then 
			print("CHAR: %c", chars=[peroles[i].val.c]);

		elsif peroles[i].tipo == 3 then 
			print("INT: %i", ints=[peroles[i].val.i]);

		done 
	done
done

def printPerol(Perol @p) {
	if p.tipo == 1 then 
		print("Perol con booleano: ", end="");
		if p.val.b then print("True"); 
		else print("False");
		done

	elsif p.tipo == 2 then 
		print("Perol con caracter: %c", chars=[p.val.c]);

	elsif p.tipo == 3 then 
		print("Perol con entero: %i", ints=[p.val.i]);
	done 
}

def swapPeroles(Perol[] @peroles, Int i, Int j) {
	let Perol aux = peroles[i];
	peroles[i] = peroles[j];
	peroles[j] = aux;
}

def holandSort(Perol[] @peroles, Int N) {
	let Int l = 0, m = 0, r = N-1;
	let Perol aux;

	while m <= r do 
		if peroles[m].tipo < 2 then 
			swapPeroles(peroles, m, l);
			l = l + 1;
			m = m + 1;
		elsif peroles[m].tipo > 2 then 
			swapPeroles(peroles, m, r);
			r = r - 1;
		else 
			m = m + 1;
		done
	done 
}